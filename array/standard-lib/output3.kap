namespace("o3")

⍝ ┌┌┌→──┐
⍝ ↓↓↓0 1│
⍝ │││2 3│
⍝ ││├→──┤
⍝ ││↓4 5│
⍝ │││6 7│
⍝ │├├→──┤
⍝ │↓↓0 1│
⍝ │││2 3│
⍝ ││├→──┤
⍝ ││↓4 5│
⍝ │││6 7│
⍝ └└└───┘

isArray ⇐ 'array≡typeof
isChar ⇐ 'char≡typeof
isSymbol ⇐ 'symbol≡typeof

isNumber ⇐ { (typeof ⍵) ∊ 'integer 'float 'complex 'rational }

⍝ Replace 0 with a space in the input array
zSpc ⇐ { (0≡¨⍵) % ⍵ @\s }

∇ isAplString (v) {
  (1≢≢⍴v) → 0
  ~0∊'char=typeof¨v
}

∇ isEnclosedArray {
  (0≠≢⍴⍵) → 0
  ⍵≢⊃⍵
}

∇ toHex {
  "0123456789ABCDEF" ⊇⍨ {⍵ ⫽⍨ ~×\0=⍵} (6 ⍴ 16) ⊤ ⍵
}

∇ renderChar ch {
  n ← ch-@\0
  ch ← if ((n ≥ 33) ∧ (n ≤ 126)) {
    @@ , ch
  } else {
    "@\\u" , toHex n
  }
  1 ¯1 ⍴ ch
}

∇ renderString {
  zSpc ⊃ (@\n≠)⍛⊂ @",⍵,@"
}

∇ renderNumber (n) {
  1 ¯1 ⍴ ⍕n
}

∇ renderSymbol (sym) {
  1 ¯1 ⍴ ⍕sym
}

∇ alignCells (v) {
  if (2≢≢⍴v) { throw "alignCells requires the argument to have rank 2" }

  ⍝ The width of each column is the maximum width of any cell
  colWidths ← ⌈⌿ (↑↓⍴)¨v
  ⍝ Pad each cell to the correct width
  xAligned ← ((⍴v) ⍴ colWidths) {((≢⍵) (⍺-↑↓⍴⍵) ⍴ @\s),⍵}¨ v

  ⍝ Likewise for the heights
  rowHeights ← ⌈/ ≢¨xAligned
  yAligned ← (⍉ (⌽ ⍴ xAligned) ⍴ rowHeights) {⍵ ,[0] ((⍺-≢⍵) (↑↓⍴⍵)) ⍴ @\s}¨ xAligned
}

∇ renderArray v0 {
  aligned ← alignCells ¯1 (↑⌽⍴v0) ⍴ v0
  (nRows nCols) ← ⍴ aligned
  f ← ({⊂ (⊃⍪/{⍺,@\s,⍵}/⍵) , @│}⍤2) (¯1 , (¯2↑1,⍴v0)) ⍴ aligned

  w ← ↑↓⍴↑f

  top ← 1 ¯1 ⍴ "→" , ((w-2) ⍴ @─) ,"┐"
  bottom ← 1 ¯1 ⍴ ((w-1) ⍴ @─) ,"┘"
  separator ← 1 ¯1 ⍴ "→" , ((w-2) ⍴ @─) , "┤"
  res ← {top⍪⍵⍪bottom} ↑ ⍪⌿⍪ ¯1↓ , f ,[0.5] ⊂separator

  ⍝ To draw the layers to the left, first determine the number of layers and their sizes.
  ds ← ¯2↓⍴v0

  ⍝ Compute the number of layer indicators for each layer
  layerPos ← ⌽1,×\ds

  makeVerticalLayer ⇐ {
    ⍝ Split content in groups of size ⍵, and compute height of each group
    sizes ← +/≢¨ ¯1 ⍵ ⍴ f
    ⍝ Create a sequence of vertical bars according to the sizes,
    ⍝ and end with a split symbol. Finally remove the last instance.
    coverage ← ⍵-1
    vertArrow ← ⍺ ⌷ "│↓"
    centre ← ¯1↓↑,/ {( vertArrow , (⍵+coverage-1) ⍴ @│) , @├}¨ sizes
    ⍪ ↑ ,/ @┌ , centre , @└
  }

  verticalAxisIndicator ← ((0⌈¯1+≢layerPos) ⍴ 0) , (1≠≢⍴v0)
  (↑,/ verticalAxisIndicator makeVerticalLayer¨ layerPos) , res
}

singleLineSyms ← "┌┐└┘─│"
doubleLineSyms ← "╔╗╚╝═║"

withBorder ⇐ {
  nCols ← ↑↓⍴⍵
  (1 nCols 1 ⫽ 0 4 1 ⊇ ⍺) ⍪ (⍺[5] , ⍵ , ⍺[5]) ⍪ (1 nCols 1 ⫽ 2 4 3 ⊇ ⍺)
}

renderEmpty ⇐ {
  2 3 ⍴ "┌⊖┐└─┘"
}

format ⇐ { throw "Forward declaration" }

renderArrayOrString ⇐ {
  v←⍵
  when {
    ((,0)≡⍴v)           { 1 1 ⍴ "⍬" }
    (0=×/⍴v)            { renderEmpty v }
    (isAplString v)     { renderString v }
    (isEnclosedArray v) { singleLineSyms withBorder format ⊃v }
    (1)                 { renderArray format ¨v }
  }
}

∇ renderMap {
  1 ¯1 ⍴ "[map size:",(⍕mapSize ⍵),"]"
}

renderList ⇐ {
  aligned ← { alignCells ¯1 (↑⌽⍴⍵) ⍴ ⍵ } format¨ fromList ⍵
  ⍝ Height of the first cell (all cells have the same height)
  h ← ↑⍴↑aligned
  withSeparators ← @║ , (⊃,/ ¯1↓ , (⍉aligned) , ⊂⍪ h ⍴ @│) , @║
  top ← @╔ , (↑,/ ¯1↓ ,(⍉ (@═ ⍴⍨ ↑⌽⍴)¨ aligned) , @╤) , @╗
  bottom ← @╚ , (↑,/ ¯1↓ ,(⍉ (@═ ⍴⍨ ↑⌽⍴)¨ aligned) , @╧) , @╝
  top ⍪ withSeparators ⍪ bottom
} 

typeToFormatter ← map `
        'integer  λrenderNumber `
        'float    λrenderNumber `
        'complex  λrenderNumber `
        'rational λrenderNumber `
        'array    λrenderArrayOrString `
        'char     λrenderChar `
        'symbol   λrenderSymbol `
        'map      λrenderMap `
        'list     λrenderList

format ⇐ {
  v←⍵
  renderer ← typeToFormatter[typeof v]
  ⍞((⍬≡renderer) ⌷ renderer λ{1 ¯1 ⍴ ⍕typeof ⍵}) v
}

'kap:renderer sysparam λformat
